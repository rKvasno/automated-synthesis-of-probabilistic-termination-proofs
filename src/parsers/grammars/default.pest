program = { SOI ~ statements ~ invariant? ~ NEWLINE* ~ EOI }
statement = { invariant? ~ statement_helper }

invariant = { ("[" ~ NEWLINE* ~ logic_condition ~ NEWLINE* ~ "]" ~ NEWLINE* )+ }

statements = { NEWLINE* ~ statement? ~ (NEWLINE+ ~ statement)* ~ NEWLINE* }

block = _{ NEWLINE? ~ "{" ~ statements ~ "}" }

statement_helper = _{ "if" ~ if_statement         |
                      "odds" ~ odds_statement     | 
                      "choose" ~ choose_statement |
                      "while" ~ while_statement   |
                      sampling_statement          |
                      assignment_statement        }

if_statement = { if_helper }
if_helper = _{ logic_condition ~ block ~ ( NEWLINE? ~ "else" ~ NEWLINE? ~ else)? }
else =  _{ "if " ~ if_helper |
           block             }

odds_statement = { odds_helper }
odds_helper = _{ constant ~ ":" ~ ( odds_helper | constant ) ~ block }

choose_statement = { block ~ ( NEWLINE? ~ "or" ~  block )*}
while_statement = { condition ~ block }
sampling_statement = { variable ~ "=" ~ "random(" ~ constant ~ "," ~ constant ~ "," ~ constant ~ ")" }
assignment_statement = { variable ~ "=" ~ linear_polynomial}

condition = _{ logic_condition     |
               odds_condition      |
               nondeterminism_sign }
logic_condition = { inequality ~ ( NEWLINE? ~ "and" ~ NEWLINE? ~ inequality )* }
odds_condition = { constant ~ ":" ~ constant }
nondeterminism_sign = @{ "*" }

inequality = { linear_polynomial ~ comparison_op ~ linear_polynomial }
comparison_op = @{ "<=" | ">=" | "<" | ">" }

COMMENT = _{ "#" ~ (!NEWLINE ~ ANY)* ~ NEWLINE }
